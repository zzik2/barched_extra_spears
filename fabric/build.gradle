plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    // Barched
    modImplementation("maven.modrinth:barched:${project.barched_fabric_version}")
    modApi("me.shedaniel.cloth:cloth-config-fabric:$rootProject.cloth_config_version") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    // External Mods
    modImplementation("maven.modrinth:bronze:${project.bronze_fabric_version}")
//    modImplementation("maven.modrinth:paradise-lost:${project.paradise_lost_fabric_version}")
    modImplementation(files(rootProject.file("libs/paradise-lost-${project.paradise_lost_fabric_version}.jar"))) //TODO: delete

    //paradise-lost dependencies
    modRuntimeOnly("net.kyrptonaught:customportalapi:0.0.1-beta67-1.21")
    modRuntimeOnly("dev.thomasglasser.sherdsapi:sherdsapi-fabric-1.21.1:6.0.2")
    modRuntimeOnly("com.github.Chocohead:Fabric-ASM:v2.3")
    modRuntimeOnly("org.ladysnake.cardinal-components-api:cardinal-components-base:6.1.0")
    modRuntimeOnly("org.ladysnake.cardinal-components-api:cardinal-components-entity:6.1.0")

    // Utility Mods
    modRuntimeOnly("maven.modrinth:jei:${project.jei_fabric_version}")
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
